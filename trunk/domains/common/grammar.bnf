#BNF+EMV2.0;

!grammar UserEval;
!start <Top>;

<Top>: <Move1> | <Move2> | <Move3> | <PickUp> | <Release> | <Perception> | <Grounding> | <Repeat> | stop | <Opening> | <Closing> | <Other> ;

<Front>: !optional(and) !optional(could you) !optional(now) !optional(please) ;

<Back>: !optional(please) !optional(again) ;

<Move1>: <Front> !optional(<Move4>) !optional(<Modif>) (forward | forwards | straight forward | backward | backwards | back | straight | straight ahead) !optional(<Modif>) <Back>;
<Move2>: <Front> !optional(<Move5>)  !optional(<Modif>) !optional(to the) (left | right) !optional(<Modif>) <Back>;
<Move3>: <Front> (turn back | turn one hundred eighty degrees | make a u turn | turn around) <Back>;

<Move4>: walk | go | move | continue ;
<Move5>: go | move | turn | rotate ;

<Modif>: !optional(just) a !optional(little) bit !optional(more);

<PickUp>: <Front> (take <Object> | take it | pick up <Object> | grasp <Object> | grasp it | pick it up) <Back>;

<Release>: <Front> ((release | put down | drop) <Object> | put <Object> (down | on the floor | on the table)) <Back>;

<Object>: something | (the | a) !optional(<Colour>) (food can | object | cylinder) !optional(at your feet);

<Colour>: red | blue | green | yellow | white | black ;

<Perception>: what (can | do) you (see | perceive) | (can | do) you (see | perceive) <Object>;

<Grounding>: yes !optional(please) | no | exactly | ok ;

 <Other>: thanks !optional(<Name>) | thank you !optional(<Name>) | !optional(<Front2>) (correct | incorrect | wrong | right | perfect | great | good);
 
<Front2>: that's | that is | this is ;

<Repeat>: <Front> do (it | that) (again | once more | one more time) | <Front> repeat that;

<Opening>: (hi | hello ) <Name>;

<Closing>: (bye | goodbye) !optional(<Name>);

<Name>: robot | nao | lenny;